{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Coordinate Transformations"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "8.881784197001252e-16"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    },
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "(9968, 3)\n"
     ]
    }
   ],
   "source": [
    "# global constants\n",
    "g_maxAngVel = pi / 2 # move in azimuth or elevation at max rate of 90 deg / sec\n",
    "g_maxScanRate = 1000 # scans points at the rate of 1000 pts/sec\n",
    "\n",
    "# function to convert points from cartesian Part Coordiante System to cartesian Machine Coordinate System \n",
    "# input:  mcs: 4x4 transformation matrix\n",
    "#         pts: nx3 list of points in PCS\n",
    "# output: nx3 list of points in MCS\n",
    "function pcs_to_mcs_xyz(mcs, pts)\n",
    "    # transform all points into LR Cartesian coordinates\n",
    "    # homogenize the pts with a 1\n",
    "    qty = size(pts,1)\n",
    "    tmp = [pts ones(qty,1)]\n",
    "    # multiply by inverse of the LR's location/orientation\n",
    "    ret = inv(mcs) * tmp'\n",
    "    return ret[1:3,:]'\n",
    "end;\n",
    "\n",
    "# function to convert points from cartesian Part Coordiante System to cartesian Machine Coordinate System \n",
    "# input:  mcs: 4x4 transformation matrix\n",
    "#         pts: nx3 list of points in MCS\n",
    "# output: nx3 list of points in PCS\n",
    "function mcs_to_pcs_xyz(mcs, pts)\n",
    "    # transform all points into LR Cartesian coordinates\n",
    "    # homogenize the pts with a 1\n",
    "    qty = size(pts,1)\n",
    "    tmp = [pts ones(qty,1)]\n",
    "    # multiply by inverse of the LR's location/orientation\n",
    "    ret = mcs * tmp'\n",
    "    return ret[1:3,:]'\n",
    "end;\n",
    "\n",
    "# function to convert points from cartesian to spherical\n",
    "# input:  pts: nx3 list of xyz (cartesian) points\n",
    "# output: nx3 list of spherical points (range, theta, phi)\n",
    "function xyz_to_sph(pts)\n",
    "    r = [vecnorm(pts[i,:]) for i=1:size(pts,1)]\n",
    "    r2= [vecnorm(pts[i,1:2]) for i=1:size(pts,1)]\n",
    "    t = atan2.(r2, pts[:,3])\n",
    "    p = atan2.(pts[:,2], pts[:,1])\n",
    "    return [r t p]\n",
    "end;\n",
    "\n",
    "# function to convert points from spherical to cartesian\n",
    "# input:  pts: nx3 list of (r,t,p) (spherical) points\n",
    "# output: nx3 list of cartesian points (xyz)\n",
    "function sph_to_xyz(pts)\n",
    "    # multiply by inverse of the LR's location/orientation\n",
    "    x = pts[:,1] .* sin.(pts[:,2]) .* cos.(pts[:,3]) # rsinθcosϕ\n",
    "    y = pts[:,1] .* sin.(pts[:,2]) .* sin.(pts[:,3]) # rsinθsinϕ\n",
    "    z = pts[:,1] .* cos.(pts[:,2])                   # rcosθ\n",
    "    return [x y z]\n",
    "end;\n",
    "\n",
    "\n",
    "# function to simulate a scan from one spherical coord to another\n",
    "# input: from: spherical coordinate triple (radius, theta, phi) to start from\n",
    "#        to:   spherical coordinate triple (radius, theta, phi) to end at\n",
    "# output: n x 3 list of spherical points\n",
    "function scan_from_to(from, to)\n",
    "    delta = to .- from\n",
    "    time = maximum(abs.(delta[2:3])) / g_maxAngVel\n",
    "    qty = floor(time * g_maxScanRate)\n",
    "    rgs = linspace.(from, to, maximum([qty,5])) # want at least 5 points.\n",
    "    return [collect(rgs[1]) collect(rgs[2]) collect(rgs[3])]\n",
    "end\n",
    "\n",
    "# function to simulate a scan through a list of (spherical) points\n",
    "# input:  list of points (spherical) in the order to be visited\n",
    "# output: list of points (spherical) of the simulated scan points\n",
    "function scan_tour(pts)\n",
    "    ret = []\n",
    "    for i = 1:size(pts,1)-1\n",
    "        line = scan_from_to(pts[i,:], pts[i+1,:])\n",
    "        ret = [ret; line]\n",
    "    end\n",
    "    return ret\n",
    "end\n",
    "\n",
    "function test_transforms()\n",
    "    # load the feature file\n",
    "    data = readcsv(\"50_Holes_FrontLeft.csv\");\n",
    "    pts_pcs = convert(Array{Float64, 2}, data[:,2:4]) # feature locations in PCS\n",
    "    lr_tfm = readcsv(\"Location1.csv\") # transformation matrix for LR's position\n",
    "\n",
    "    # convert to spherical\n",
    "    pts_mcs = pcs_to_mcs_xyz(lr_tfm, pts_pcs) # convert feature locations to MCS\n",
    "    pts_lr = xyz_to_sph(pts_mcs) # convert feature locations to spherical\n",
    "\n",
    "    # round trip as a test\n",
    "    pts_mcs_back = sph_to_xyz(pts_lr)\n",
    "    pts_pcs_back = mcs_to_pcs_xyz(lr_tfm, pts_mcs_back)\n",
    "\n",
    "    # points should be the same\n",
    "    diff = pts_pcs - pts_pcs_back\n",
    "    display(maximum(diff))\n",
    "end\n",
    "\n",
    "function test_scan()\n",
    "    # load data.\n",
    "    data = readcsv(\"50_Holes_FrontLeft.csv\");\n",
    "    pts_pcs = convert(Array{Float64, 2}, data[:,2:4]) # feature locations in PCS\n",
    "    lr_tfm = readcsv(\"Location1.csv\") # transformation matrix for LR's position\n",
    "\n",
    "    # shuffle an index vector\n",
    "    idx = Vector(1:size(pts_pcs,1))\n",
    "    rng = MersenneTwister(1234)\n",
    "    idx = shuffle(rng, idx)\n",
    "    \n",
    "    # shuffle input pts and convert them to spherical\n",
    "    sph = xyz_to_sph(pcs_to_mcs_xyz(lr_tfm, pts_pcs[idx,:]))\n",
    "\n",
    "    # generate scan data\n",
    "    scan = scan_tour(sph)\n",
    "    \n",
    "    # back to pcs\n",
    "    scan = mcs_to_pcs_xyz(lr_tfm, sph_to_xyz(scan))\n",
    "    println(size(scan))\n",
    "    \n",
    "    # save\n",
    "    writecsv(\"scan1.csv\", scan)\n",
    "end\n",
    "\n",
    "test_transforms()\n",
    "test_scan()"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## TSP"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {
    "scrolled": false
   },
   "outputs": [],
   "source": [
    "using JuMP, Gurobi\n",
    "\n",
    "function find_subtour(edges, n)\n",
    "    visited = [false for i=1:n]\n",
    "    cycles=[]\n",
    "    lengths=[]\n",
    "    selected=[[] for i=1:n]\n",
    "    for e in edges\n",
    "        push!(selected[e[1]], e[2])\n",
    "    end\n",
    "    while true\n",
    "        current = find(visited .== false)[1]\n",
    "        thiscycle = [current]\n",
    "        while true\n",
    "            visited[current] = true\n",
    "            neighbors = []\n",
    "            for x in selected[current]\n",
    "                if visited[x] == false\n",
    "                    push!(neighbors, x)\n",
    "                end\n",
    "            end\n",
    "            if length(neighbors) == 0\n",
    "                break\n",
    "            end\n",
    "            current = neighbors[1]\n",
    "            push!(thiscycle, current)\n",
    "        end\n",
    "        push!(cycles, thiscycle)\n",
    "        push!(lengths, length(thiscycle))\n",
    "        if sum(lengths) == n\n",
    "            break\n",
    "        end\n",
    "    end\n",
    "    #display(cycles)\n",
    "    return cycles[indmin(lengths)]\n",
    "end\n",
    "\n",
    "function remove_dummy_node(tour)\n",
    "    len = length(tour)\n",
    "    idx_dummy = indmax(tour)\n",
    "    return [tour[(i+idx_dummy)%len+1] for i=0:len-2]\n",
    "end\n",
    "\n",
    "function tsp(cost, return_to_start)\n",
    "    n = size(cost,1)\n",
    "    if !return_to_start\n",
    "        # if we don't need to form a loop, add a dummy node of cost 0 to everywhere.\n",
    "        # our non-cyclic path will start from the node following this node\n",
    "        # and end at the node preceding this node.\n",
    "        cost = [cost zeros(size(cost,1),1)]\n",
    "        cost = [cost ; zeros(1, size(cost,2))]\n",
    "        n += 1\n",
    "    end\n",
    "\n",
    "    # TSP solve\n",
    "    m = Model(solver=GurobiSolver(OutputFlag=0))    \n",
    "    # use the symmetric matrix formulation & subtour elimination described at\n",
    "    #  http://examples.gurobi.com/traveling-salesman-problem/\n",
    "    @variable(m, z[i=1:n, j=1:n], Bin, Symmetric)\n",
    "    @objective(m, Min, 0.5*sum(z.*cost)) # minimize the sum of movement times\n",
    "    @constraint(m, [i in 1:n], sum(z[i,j] for j=1:n) == 2 ) # one in and one out edge\n",
    "    @constraint(m, [ii in 1:n], z[ii,ii] == 0) # can't transition to self\n",
    "\n",
    "    num_lazy_constraints = 0\n",
    "    function subtour_callback(cb)\n",
    "        soln = getvalue(z)\n",
    "        # check for subtours\n",
    "        selected = [[i,j] for i=1:n, j=1:n if soln[i,j] > 0.5]\n",
    "        subtour = find_subtour(selected, n)\n",
    "        if length(subtour) == n\n",
    "            return\n",
    "        end\n",
    "        # add constraint to prohibit these subtours\n",
    "        len = length(subtour)\n",
    "        subtour = sort(subtour)\n",
    "        num_lazy_constraints += 1\n",
    "        @lazyconstraint(cb, sum( z[subtour[j],subtour[i]] for i = 1:len, j=i:len ) <= len-1)\n",
    "    end\n",
    "\n",
    "    # add the lazy constraint callback and solve\n",
    "    addlazycallback(m, subtour_callback)\n",
    "    solve(m)\n",
    "    soln = getvalue(z)\n",
    "                \n",
    "    # extract feature order\n",
    "    selected = [[i,j] for i=1:n, j=1:n if soln[i,j] > 0.5]\n",
    "    tour = find_subtour(selected, n)\n",
    "    if !return_to_start\n",
    "        tour = remove_dummy_node(tour)\n",
    "    end\n",
    "    return [tour, getobjectivevalue(m)]\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Driver"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Academic license - for non-commercial use only\n",
      "Feature path without optimization: 11.792777 sec\n",
      "Feature path with optimization:    1.561622 sec\n",
      "Time savings:                      10.231154 sec\n",
      "Optimal feature ordering:\n",
      "[29, 14, 35, 39, 24, 41, 20, 12, 21, 49, 47, 18, 23, 36, 28, 32, 45, 37, 1, 10, 46, 33, 44, 25, 27, 15, 5, 34, 50, 48, 3, 19, 40, 13, 4, 43, 7, 22, 8, 17, 38, 6, 2, 31, 30, 26, 9, 42, 16, 11]\n",
      "Computational time to optimize:    4.079195 sec\n",
      "Writing 11771 non-optimized scan points to '50_left_front_scan_no_opt.csv'\n",
      "Writing 1541 optimized scan points to '50_left_front_scan_opt.csv'\n"
     ]
    }
   ],
   "source": [
    "# function that computes the travel time from spherical coordinate 'from' to 'to'\n",
    "# (assumes instantaneous acceleration to max velocity)\n",
    "travel_time(from, to) = maximum(abs.((to .- from)[2:3])) / g_maxAngVel\n",
    "\n",
    "# function that takes a list of nx3 spherical coordinates and returns an n x n \n",
    "# matrix where A(i,j) where  the time it takes to move from point i to point j\n",
    "cost_matrix(sph) = [travel_time(sph[i,:],sph[j,:]) for i=1:size(sph,1), j=1:size(sph,1)]\n",
    "\n",
    "# finds minimal path to scan features given an LR location, and\n",
    "# computes (simulated) scan data along that path.\n",
    "# input:  features: matrix of features in feature file format.\n",
    "#         lr_location: 4x4 transformation matrix of LR's position in PCS.\n",
    "#         optimize: boolean indicating whether to optimize the path or not.\n",
    "# output: list containing:\n",
    "#           list of indices of order that the features were measured\n",
    "#           time it took to travel to the entire path\n",
    "#           elapsed (computational) time to optimize the path\n",
    "#           simulated scan points along the path\n",
    "function scan_feature_path(features, lr_location, optimize)\n",
    "    # xyz location of features\n",
    "    pts_pcs = convert(Array{Float64, 2}, features[:,2:4]) # feature locations in PCS\n",
    "\n",
    "    # spherical coordinates with origin at laser radar\n",
    "    sph = xyz_to_sph(pcs_to_mcs_xyz(lr_location, pts_pcs))\n",
    "    cost = cost_matrix(sph) # cost to travel from feature i to feature j\n",
    "    \n",
    "    if optimize\n",
    "        # optimize\n",
    "        tm = @timed(tsp(cost, false))\n",
    "    else\n",
    "        # don't optimize. scan in current order\n",
    "        n = size(cost,1)\n",
    "        tm = @timed([[i for i=1:n], sum(cost[i,i+1] for i=1:n-1)])\n",
    "    end\n",
    "    tour = tm[1]\n",
    "    push!(tour, tm[2])\n",
    "    \n",
    "    # simulate a scan of the features\n",
    "    scan = scan_tour(sph[tour[1],:])\n",
    "    # back to pcs\n",
    "    scan = mcs_to_pcs_xyz(lr_location, sph_to_xyz(scan))\n",
    "    push!(tour, scan)\n",
    "    return tour\n",
    "end\n",
    "\n",
    "# test harness\n",
    "function test(featureFile, locationFile, outScan_no_opt, outScan_opt)            \n",
    "    features = readcsv(featureFile); # feature file\n",
    "    lr_position = readcsv(locationFile) # transformation matrix for LR's positionend\n",
    "\n",
    "    scan_no_opt = scan_feature_path(features, lr_position, false)\n",
    "    scan_opt = scan_feature_path(features, lr_position, true)\n",
    "    \n",
    "    @printf(\"Feature path without optimization: %.6f sec\\n\", scan_no_opt[2])\n",
    "    @printf(\"Feature path with optimization:    %.6f sec\\n\", scan_opt[2])\n",
    "    @printf(\"Time savings:                      %.6f sec\\n\", scan_no_opt[2] - scan_opt[2])\n",
    "    @printf(\"Optimal feature ordering:\\n\")\n",
    "    println(scan_opt[1])\n",
    "    @printf(\"Computational time to optimize:    %.6f sec\\n\", scan_opt[3])\n",
    "    @printf(\"Writing %d non-optimized scan points to '%s'\\n\", size(scan_no_opt[4],1), outScan_no_opt)\n",
    "    @printf(\"Writing %d optimized scan points to '%s'\\n\", size(scan_opt[4],1), outScan_opt)\n",
    "    writecsv(outScan_no_opt, scan_no_opt[4])\n",
    "    writecsv(outScan_opt, scan_opt[4])\n",
    "end\n",
    "        \n",
    "test(\"50_Holes_FrontLeft.csv\", \"Location1.csv\", \"50_left_front_scan_no_opt.csv\", \"50_left_front_scan_opt.csv\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Test Different LR Locations on same Feature File"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Academic license - for non-commercial use only\n",
      "Feature path without optimization: 8.281533 sec\n",
      "Feature path with optimization:    1.213455 sec\n",
      "Time savings:                      7.068077 sec\n",
      "Optimal feature ordering:\n",
      "[11, 16, 2, 6, 42, 9, 26, 30, 31, 38, 8, 17, 43, 4, 22, 7, 13, 40, 19, 50, 5, 15, 27, 44, 25, 28, 32, 45, 36, 23, 18, 47, 49, 37, 1, 10, 46, 33, 34, 48, 3, 24, 41, 20, 12, 21, 39, 35, 14, 29]\n",
      "Computational time to optimize:    0.187134 sec\n",
      "Writing 8259 non-optimized scan points to 'ignore.csv'\n",
      "Writing 1204 optimized scan points to '50feature_LRFront.csv'\n",
      "Academic license - for non-commercial use only\n",
      "Feature path without optimization: 56.738953 sec\n",
      "Feature path with optimization:    4.856255 sec\n",
      "Time savings:                      51.882698 sec\n",
      "Optimal feature ordering:\n",
      "[11, 16, 26, 9, 42, 31, 30, 38, 17, 43, 4, 5, 13, 15, 40, 50, 19, 27, 44, 25, 45, 32, 28, 36, 23, 48, 3, 18, 47, 49, 21, 12, 20, 41, 24, 14, 29, 35, 39, 37, 1, 10, 46, 33, 34, 7, 22, 8, 6, 2]\n",
      "Computational time to optimize:    0.361217 sec\n",
      "Writing 56720 non-optimized scan points to 'ignore.csv'\n",
      "Writing 4849 optimized scan points to '50feature_LRLeft.csv'\n"
     ]
    }
   ],
   "source": [
    "# location of LR is 0,4,0\n",
    "test(\"50_Holes_FrontLeft.csv\", \"LocationFront.csv\", \"ignore.csv\", \"50feature_LRFront.csv\")\n",
    "# location of LR is 3,1,0\n",
    "test(\"50_Holes_FrontLeft.csv\", \"LocationLeft.csv\", \"ignore.csv\", \"50feature_LRLeft.csv\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.6.4",
   "language": "julia",
   "name": "julia-0.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.6.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
