{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Double The Fun!\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "link to [Double The Fun!]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, Gurobi"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# global constants\n",
    "g_maxAngVel = 1 # move in azimuth or elevation at max rate of 1 rad / sec\n",
    "g_maxScanRate = 1000 # scans points at the rate of 1000 pts/sec\n",
    "\n",
    "# function to convert points from cartesian Part Coordiante System to cartesian Machine Coordinate System \n",
    "# input:  mcs: 4x4 transformation matrix\n",
    "#         pts: nx3 list of points in PCS\n",
    "# output: nx3 list of points in MCS\n",
    "function pcs_to_mcs_xyz(mcs, pts)\n",
    "    # transform all points into LR Cartesian coordinates\n",
    "    # homogenize the pts with a 1\n",
    "    qty = size(pts,1)\n",
    "    tmp = [pts ones(qty,1)]\n",
    "    # multiply by inverse of the LR's location/orientation\n",
    "    ret = inv(mcs) * tmp'\n",
    "    return ret[1:3,:]'\n",
    "end;\n",
    "\n",
    "# function to convert points from cartesian Part Coordiante System to cartesian Machine Coordinate System \n",
    "# input:  mcs: 4x4 transformation matrix\n",
    "#         pts: nx3 list of points in MCS\n",
    "# output: nx3 list of points in PCS\n",
    "function mcs_to_pcs_xyz(mcs, pts)\n",
    "    # transform all points into LR Cartesian coordinates\n",
    "    # homogenize the pts with a 1\n",
    "    qty = size(pts,1)\n",
    "    tmp = [pts ones(qty,1)]\n",
    "    # multiply by inverse of the LR's location/orientation\n",
    "    ret = mcs * tmp'\n",
    "    return ret[1:3,:]'\n",
    "end;\n",
    "\n",
    "# function to convert points from cartesian to spherical\n",
    "# input:  pts: nx3 list of xyz (cartesian) points\n",
    "# output: nx3 list of spherical points (range, theta, phi)\n",
    "function xyz_to_sph(pts)\n",
    "    r = [vecnorm(pts[i,:]) for i=1:size(pts,1)]\n",
    "    r2= [vecnorm(pts[i,1:2]) for i=1:size(pts,1)]\n",
    "    t = atan2.(r2, pts[:,3])\n",
    "    p = atan2.(pts[:,2], pts[:,1])\n",
    "    return [r t p]\n",
    "end;\n",
    "\n",
    "# function to convert points from spherical to cartesian\n",
    "# input:  pts: nx3 list of (r,t,p) (spherical) points\n",
    "# output: nx3 list of cartesian points (xyz)\n",
    "function sph_to_xyz(pts)\n",
    "    # multiply by inverse of the LR's location/orientation\n",
    "    x = pts[:,1] .* sin.(pts[:,2]) .* cos.(pts[:,3]) # rsinθcosϕ\n",
    "    y = pts[:,1] .* sin.(pts[:,2]) .* sin.(pts[:,3]) # rsinθsinϕ\n",
    "    z = pts[:,1] .* cos.(pts[:,2])                   # rcosθ\n",
    "    return [x y z]\n",
    "end;\n",
    "\n",
    "\n",
    "# function to simulate a scan from one spherical coord to another\n",
    "# input: from: spherical coordinate triple (radius, theta, phi) to start from\n",
    "#        to:   spherical coordinate triple (radius, theta, phi) to end at\n",
    "# output: n x 3 list of spherical points\n",
    "function scan_from_to(from, to)\n",
    "    delta = to .- from\n",
    "    time = maximum(abs.(delta[2:3])) / g_maxAngVel\n",
    "    qty = floor(time * g_maxScanRate)\n",
    "    #@printf(\"Scanning from (%f,%f,%f) to (%f,%f,%f), %d points\\n\", from[1], from[2], from[3], to[1], to[2], to[3], qty)\n",
    "    rgs = linspace.(from, to, maximum([qty,5])) # want at least 5 points.\n",
    "    return [collect(rgs[1]) collect(rgs[2]) collect(rgs[3])]\n",
    "end\n",
    "\n",
    "# function to simulate a scan through a list of (spherical) points\n",
    "# input:  list of points (spherical) in the order to be visited\n",
    "# output: list of points (spherical) of the simulated scan points\n",
    "function scan_tour(pts)\n",
    "    ret = []\n",
    "    for i = 1:size(pts,1)-1\n",
    "        line = scan_from_to(pts[i,:], pts[i+1,:])\n",
    "        ret = [ret; line]\n",
    "    end\n",
    "    return ret\n",
    "end\n",
    "\n",
    "# function that computes the travel time from spherical coordinate 'from' to 'to'\n",
    "# (assumes instantaneous acceleration to max velocity)\n",
    "travel_time(from, to) = maximum(abs.((to .- from)[2:3])) / g_maxAngVel\n",
    "\n",
    "# function that takes a list of nx3 spherical coordinates and returns an n x n \n",
    "# matrix where A(i,j) where  the time it takes to move from point i to point j\n",
    "cost_matrix(sph) = [travel_time(sph[i,:],sph[j,:]) for i=1:size(sph,1), j=1:size(sph,1)]\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 79,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Academic license - for non-commercial use only\r\n",
      "LR1 measures path: [1, 10, 15, 13, 4, 7, 17, 8, 9, 6, 2, 16, 11] in 0.6354611540272634 seconds.\n",
      "LR2 measures path: [14, 12, 18, 3, 5, 19] in 0.6239931571551316 seconds.\n",
      "155.177331 seconds (585.58 k allocations: 51.152 MiB)\n"
     ]
    }
   ],
   "source": [
    "function TSP2_LazyIterSubtour(cost1, cost2)\n",
    "    n = size(cost1,1) # number of features\n",
    "    # to form a path, add a dummy node of cost 0 to everywhere.\n",
    "    # add two dummy nodes because we have 2 laser radars\n",
    "    for i = 1:2\n",
    "        cost1 = [cost1 zeros(size(cost1,1),1)]\n",
    "        cost1 = [cost1 ; zeros(1, size(cost1,2))]\n",
    "        cost2 = [cost2 zeros(size(cost2,1),1)]\n",
    "        cost2 = [cost2 ; zeros(1, size(cost2,2))]\n",
    "        n += 1\n",
    "    end\n",
    "\n",
    "    m = Model(solver=GurobiSolver(OutputFlag=0))\n",
    "    @variable(m, z1[1:n, 1:n], Bin) # LR1 path\n",
    "    @variable(m, z2[1:n, 1:n], Bin) # LR2 path\n",
    "    @variable(m, s[1:n], Bin) # selects LR1 or LR2 for each feature\n",
    "    @variable(m, t >= 0) # for epigraph trick\n",
    "    \n",
    "    @objective(m, Min, t) # for epigraph trick to minimize maximum path cost\n",
    "    @constraint(m, sum(s.*z1.*cost1) <= t) # cost of LR1 is less than t\n",
    "    @constraint(m, sum((1-s).*z2.*cost2) <= t) # cost of lR2 is less than t\n",
    "    \n",
    "    # first LR's tour must not use the second dummy node (n)\n",
    "    @constraint(m, [i in 1:n-1], sum(z1[i,:]) == s[i]) # leave each feature once\n",
    "    @constraint(m, [j in 1:n-1], sum(z1[:,j]) == s[j]) # arrive at each feature once\n",
    "    @constraint(m, sum(z1[n,:]) == 0) # force z1 to not take second dummy node\n",
    "    @constraint(m, [ii in 1:n], z1[ii,ii] == 0) # can't transition to self\n",
    "\n",
    "    # second LR's tour must not use the first dummy node (n-1)\n",
    "    for i=1:n \n",
    "        if i != n-1\n",
    "            @constraint(m, sum(z2[i,:]) == (1-s[i])) # leave each feature once\n",
    "        end\n",
    "    end\n",
    "    for j = 1:n\n",
    "        if j != n-1\n",
    "            @constraint(m, sum(z2[:,j]) == (1-s[j])) # arrive at each feature once\n",
    "        end\n",
    "    end     \n",
    "    @constraint(m, sum(z2[n-1,:]) == 0) # force z2 to not take first dummy node\n",
    "    @constraint(m, [ii in 1:n], z2[ii,ii] == 0) # can't transition to self\n",
    "    \n",
    "    num_additional_constraints = 0\n",
    "    function find_subtour(idxs,start)\n",
    "        subtour = [start]\n",
    "        while true\n",
    "            next = idxs[subtour[end]]\n",
    "            push!(subtour,next)\n",
    "            if start == next\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "        return subtour\n",
    "    end\n",
    "\n",
    "    function find_subtours2(idxs)\n",
    "        remaining = filter(x->x!=0, idxs)\n",
    "        subtours = []\n",
    "        while length(remaining) > 0 \n",
    "            subtour = find_subtour(idxs, remaining[1])\n",
    "            push!(subtours, subtour)\n",
    "            remaining = setdiff(remaining, subtour)\n",
    "        end\n",
    "        return subtours\n",
    "    end\n",
    "    \n",
    "    function check_for_subtours(z, cb)\n",
    "        soln = getvalue(z)\n",
    "        # check for subtours\n",
    "        idxs = []\n",
    "        for i = 1:n\n",
    "            lst = find(soln[i,:] .>= 0.5)\n",
    "            push!(idxs, length(lst) > 0 ? lst[1] : 0)\n",
    "        end\n",
    "        subtours = find_subtours2(idxs)\n",
    "        if length(subtours) == 1\n",
    "            solved = true;\n",
    "        else\n",
    "            # add constraints to prohibit these subtours\n",
    "            for subtour in subtours\n",
    "                #println(\"lazy eliminate subtour: \", subtour)\n",
    "                len = length(subtour)\n",
    "                # prohibit these particular features from being a tour\n",
    "                @lazyconstraint(cb, sum( z[subtour[i],subtour[i+1]] for i = 1:len-1 ) <= len-2)\n",
    "                # and prohibit the reverse\n",
    "                @lazyconstraint(cb, sum( z[subtour[i+1],subtour[i]] for i = 1:len-1 ) <= len-2)\n",
    "                num_additional_constraints += 2\n",
    "            end            \n",
    "        end        \n",
    "    end\n",
    "        \n",
    "    function subtour_callback(cb)\n",
    "        check_for_subtours(z1,cb)\n",
    "        check_for_subtours(z2,cb)\n",
    "    end\n",
    "\n",
    "    function remove_dummy_node(tour)\n",
    "        len = length(tour)\n",
    "        idx_dummy = indmax(tour)\n",
    "        return [tour[(i+idx_dummy)%len+1] for i=0:len-2]\n",
    "    end\n",
    "\n",
    "    function get_tour(soln, cost, choice)\n",
    "        idxs= []\n",
    "        for i = 1:n\n",
    "            lst = find(soln[i,:] .>= 0.5)\n",
    "            push!(idxs, length(lst) > 0 ? lst[1] : 0)\n",
    "        end\n",
    "        tour = find_subtours(idxs)[1]\n",
    "        tour = remove_dummy_node(tour[1:end-1])\n",
    "        return [tour, sum(choice.*soln.*cost)]\n",
    "    end\n",
    "    \n",
    "    addlazycallback(m, subtour_callback)\n",
    "    solve(m)\n",
    "    \n",
    "    # extract feature order\n",
    "    choice = getvalue(s)\n",
    "    t1 = get_tour(getvalue(z1), cost1, choice)\n",
    "    t2 = get_tour(getvalue(z2), cost2, (1.-choice))\n",
    "    return [t1, t2]\n",
    "end;\n",
    "\n",
    "# test harness\n",
    "function test2(featureFile, locationFile1, locationFile2)            \n",
    "    features = readcsv(featureFile); # feature file\n",
    "    pts_pcs = convert(Array{Float64, 2}, features[:,2:4]) # feature locations in PCS\n",
    "\n",
    "    lr1_position = readcsv(locationFile1) # transformation matrix for LR's positionend\n",
    "    lr2_position = readcsv(locationFile2) # transformation matrix for LR's positionend\n",
    "\n",
    "    start = 1\n",
    "    num = 18\n",
    "    # spherical coordinates with origin at laser radar\n",
    "    sph1 = xyz_to_sph(pcs_to_mcs_xyz(lr1_position, pts_pcs))\n",
    "    cost1 = cost_matrix(sph1[start:start+num,:]) # cost to travel from feature i to feature j\n",
    "\n",
    "    sph2 = xyz_to_sph(pcs_to_mcs_xyz(lr2_position, pts_pcs))\n",
    "    cost2 = cost_matrix(sph2[start:start+num,:]) # cost to travel from feature i to feature j\n",
    "    \n",
    "    ret = TSP2_LazyIterSubtour(cost1, cost2)\n",
    "    println(\"LR1 measures path: \", ret[1][1], \" in \", ret[1][2], \" seconds.\")\n",
    "    scan = mcs_to_pcs_xyz(lr1_position, sph_to_xyz(scan_tour(sph1[ret[1][1],:])))\n",
    "    fname = @sprintf(\"MultiTSP_LR%d_Scan_%d-%d.csv\", 1, start, start+num)\n",
    "    writecsv(fname, scan)\n",
    "\n",
    "    println(\"LR2 measures path: \", ret[2][1], \" in \", ret[2][2], \" seconds.\")\n",
    "    scan = mcs_to_pcs_xyz(lr2_position, sph_to_xyz(scan_tour(sph2[ret[2][1],:])))\n",
    "    fname = @sprintf(\"MultiTSP_LR%d_Scan_%d-%d.csv\", 2, start, start+num)\n",
    "    writecsv(fname, scan)\n",
    "end\n",
    "        \n",
    "@time(test2(\"50_Holes_FrontLeft.csv\", \"LocationFront.csv\", \"LocationLeft.csv\"))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "link to [Double The Fun!](#Double-The-Fun!)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.6.4",
   "language": "julia",
   "name": "julia-0.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.6.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
